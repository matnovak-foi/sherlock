/*
 * Copyright © 1999-2000 The University of Warwick. All Rights Reserved.
 */

/**
 * JavaCC source for a Java/C++ comment extractor.
 *
 * Need to think of a solution (lookahead = 2 and have <hashLine> as a
 * do nothing option?) for if #line appears in normal code.
 * Based on a file written by Ben Hart and Mike Joy.
 *
 * @author Daniel White
 * @version 12 July 2000
 */
options {
  IGNORE_CASE = true;
}

PARSER_BEGIN(SentenceParser)

package uk.ac.warwick.dcs.cobalt.sherlock.freetext;

import java.io.*;
import java.util.Date;

/**
 * Parses a source-code file and places all the comments into sentence objects
 * which are in turn held by a document object.
 */
public class SentenceParser {

  /**
   * The stream used to save the output file.
   */
  static ObjectOutputStream save;

  static Document currentDoc = new Document();
  static Sentence currentSentence;

  /**
   * The new file containing the output of this tokeniser.
   */
  static File outputFile = null;

  /**
   * Keep track of the line number from the file read in. Used to print #line xxx in the outputFile.
   */
  static int lineNo = 1;

  static String docName = "";

  /**
   * Reinitialise all the variables, and get on with parsing the whatever's in the input stream.
   *
   * @param is - the input stream to parse.
   * @param f - the file to save the results of this tokeniser to.
   */
  public static void ReInit(InputStream is, File f, String docName) {
    SentenceParser.currentDoc = new Document();
    SentenceParser.currentSentence = null;
    SentenceParser.outputFile = f;
    SentenceParser.lineNo = 1;
    SentenceParser.docName = docName;
    SentenceParser.ReInit(is);
  } // ReInit


  public static void main(String args[]) throws ParseException {
    SentenceParser parser = new SentenceParser(System.in);
    parser.Input();
    Document doc = SentenceParser.currentDoc;
    System.out.println(doc);
  }
}
PARSER_END(SentenceParser)


TOKEN_MGR_DECLS : {
  static boolean justStartedSentence = false;
  static void wordParsed() {
    if(justStartedSentence){
      justStartedSentence = false;
      SentenceParser.currentSentence =
        SentenceParser.currentDoc.startSentence(SentenceParser.lineNo);
    }
    SentenceParser.currentSentence.addWord(image.toString());
}
}

TOKEN : {
  <singleLineComment: "//" > {
    justStartedSentence = true;
  } : inSingleLineComment
}

TOKEN : {
  <newLine : "\r\n" | "\r" | "\n">
}

<inSingleLineComment> SKIP : {
  < <newLine> > {
    SentenceParser.lineNo++;
    justStartedSentence = true;
  } : DEFAULT
|
  < <word>"."<word> > {
    wordParsed();
  }
|
  < <word>". " | <word>"." > {
    if(justStartedSentence){
      SentenceParser.currentSentence =
        SentenceParser.currentDoc.startSentence(SentenceParser.lineNo);
    }
    SentenceParser.currentSentence.addWord(
        image.substring(0, image.lastIndexOf(".")));
    justStartedSentence = true;
  } : inSingleLineComment
|
  < <word> > {
    wordParsed();
  } : inSingleLineComment
|
  < <others> > {
        // Do Nothing.
  } : inSingleLineComment
}

TOKEN : {
  <formalComment : "/**" ~["/"]> {
    justStartedSentence = true;
    input_stream.backup(1);
  } : inFormalOrMultiLineComment
}

TOKEN : {
  <multiLineComment : "/*" > {
    justStartedSentence = true;
  } : inFormalOrMultiLineComment
}

<inFormalOrMultiLineComment> SKIP : {
  <endFormalOrMultiLineComment : "*/" > {
    justStartedSentence = true;
  } : DEFAULT
|
  < <newLine> > {
    SentenceParser.lineNo++;
  } : inFormalOrMultiLineComment
|
  < <word>"."<word> > {
    wordParsed();
  }
|
  < <word> "." <newLine> > {
    if(justStartedSentence){
      SentenceParser.currentSentence =
        SentenceParser.currentDoc.startSentence(SentenceParser.lineNo);
    }
    String temp = image.substring(0, image.lastIndexOf("."));
    SentenceParser.currentSentence.addWord(temp);
    justStartedSentence = true;
    SentenceParser.lineNo++;
  }
|
  < <word>". " | <word>"." > {
    if(justStartedSentence){
      SentenceParser.currentSentence =
        SentenceParser.currentDoc.startSentence(SentenceParser.lineNo);
    }
    SentenceParser.currentSentence.addWord(
        image.substring(0, image.lastIndexOf(".")));
    justStartedSentence = true;
  }
|
  < <word> > {
    wordParsed();
  } : inFormalOrMultiLineComment
|
  < <others> > {
    // Do nothing
  } : inFormalOrMultiLineComment
}

TOKEN : {
  <hashLine : "#line ">
| <number : ( ["0" - "9"] ) +>
}

TOKEN : {
  // The weird construction of this expression is so that words
  // like contractions (eg. it's or I'm) will be kept
  // together on a line.
  // I've allowed a word ending in a dash to be a seperate word for now
  // but later I should make it so that it concatenates the word on the
  // next line.
  <word : (<subword>)+ ((["'"])+ (<subword>)+)* ("-")* >
| <#subword :["A"-"Z", "a"-"z", "-"](["A"-"Z", "a"-"z", "0"-"9", "-"])* >
| <space : ( [" "] )+ >
}
TOKEN : {
  <others : ~[] >
|
< character_literal:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
  >
|
  < string_literal:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}
/**
 * Handle any tokens during the input.
 */
void anyOldLines () : {
}
{
  <singleLineComment>
|
  <formalComment>
|
  <multiLineComment>
|
  <hashLine><number> {
      lineNo = Integer.parseInt(SentenceParser.getToken(0).image);
    // The newLine after will incorrectly add 1 to the line count, so correct this in advance.
    // Must remember, the number refers to the original number of the *following* line.
    lineNo--;
    }
|
  <newLine> {
      lineNo++;
    }
| <number> | <word> | <space> | <character_literal> | <string_literal>
|
  <others> // Do nothing.
}


void Input () : {

  try {
    if (outputFile!= null)
      save = new ObjectOutputStream(
                             new FileOutputStream(outputFile));
    else{
      File f = new File(System.getProperty("user.home"), "testing");
      save = new ObjectOutputStream(
                             new FileOutputStream(f));
    }
  } catch (IOException e) {
      Date day = new Date(System.currentTimeMillis());
      if (outputFile != null) {
        try {
            String file = new String
                (System.getProperty("user.home") + File.pathSeparator
                 + "sherlock.log");
            BufferedWriter bw = new BufferedWriter
                (new FileWriter(file, true));
            bw.write(day + "-" + "Cannot write to: "
                     + outputFile.getAbsolutePath());
            bw.newLine();
            bw.close();
        }
        catch (IOException e2) {
            if (outputFile != null)
                System.err.println(day + "-" + "Cannot write to: "
                                   + outputFile.getAbsolutePath());
        }
      }
      else System.err.println(day + "-" + "Cannot write to System.out");
      return;
  }
}
{
  (anyOldLines())* <EOF> {
    // Tidy the document object
    SentenceParser.currentDoc.finishedParsing();
    SentenceParser.currentDoc.setName(SentenceParser.docName);
    try{
      // Write it to the output file.
      save.writeObject(SentenceParser.currentDoc);

      // Finish up.
      save.flush();
      save.close();
    } catch(IOException ioe){
      System.err.println("Error writing to file "+SentenceParser.outputFile);
    }
  }
}

